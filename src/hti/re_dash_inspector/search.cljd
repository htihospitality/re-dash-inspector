(ns hti.re-dash-inspector.search
  (:require ["package:flutter/material.dart" :as m]
            ["package:devtools_app_shared/ui.dart" :as ui]
            [cljd.flutter :as f]
            [clojure.string :as string]
            [hti.re-dash-inspector.debouncer :as debouncer]
            [hti.re-dash.core :as rd]
            [hti.re-dash-inspector.async :as async]))

(defn search-filter
  "Searches all keys and values containing the search text."
  [item search-text]
  (if search-text
    (-> (pr-str item)
        string/lower-case
        (string/includes? search-text))
    true))

(def view
  (f/widget

    :get {^m/TextEditingController search-controller :search-controller}

    :managed [debouncer (debouncer/->debouncer)]

    :watch [search-text (rd/subscribe [:rd.inspector/search-text])

            _ (async/defer #(when (string/blank? search-text)
                              (.-text! search-controller "")))
            :refresh-on search-text]

    (ui/DevToolsClearableTextField
      .labelText "Search"
      .prefixIcon (m/Icon (m/Icons.search))
      .onChanged (fn [s]
                   (.debounce debouncer
                              (Duration .milliseconds 250)
                              #(rd/dispatch [:rd.inspector/set-search-text s])))
      .controller search-controller)))
